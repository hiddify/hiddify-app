plugins {
    id "com.android.application"
    id "org.jetbrains.kotlin.android"
    id "dev.flutter.flutter-gradle-plugin"
}

def javaVersion = (findProperty('javaVersion') ?: '25').toInteger()

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(javaVersion)
    }
}

kotlin {
    jvmToolchain(javaVersion)
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

boolean hasKeyStore = false

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
    hasKeyStore = true
} else {
    println "+++"
    println "No keystore defined. The app will not be signed."
    println "Create a android/key.properties file with the following properties:"
    println "storePassword"
    println "keyPassword"
    println "keyAlias"
    println "storeFile"
    println "+++"
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode') ?: '1'
def flutterVersionName = localProperties.getProperty('flutter.versionName') ?: '1.0'

android {
    namespace 'com.hiddify.hiddify'
    testNamespace "test.com.hiddify.hiddify"
    compileSdk 36
    ndkVersion "27.3.13750724"

    compileOptions {
        sourceCompatibility JavaVersion.toVersion(javaVersion)
        targetCompatibility JavaVersion.toVersion(javaVersion)
        coreLibraryDesugaringEnabled true
    }

    kotlinOptions {
        jvmTarget = javaVersion.toString()
        freeCompilerArgs += [
            '-opt-in=kotlin.RequiresOptIn',
            '-Xjvm-default=all'
        ]
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        applicationId "app.hiddify.com"
        
        minSdk 26
        
        targetSdk 36
        
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
        
        multiDexEnabled true
        
        resourceConfigurations += ['en', 'fa']
        
        vectorDrawables.useSupportLibrary = true
    }

    splits {
        abi {
            enable true
            reset()
            //noinspection ChromeOsAbiSupport
            include "x86_64", "armeabi-v7a", "arm64-v8a"
            universalApk true
        }
    }

    if (hasKeyStore) {
        signingConfigs {
            release {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
                enableV1Signing true
                enableV2Signing true
                enableV3Signing true
                enableV4Signing true
            }
        }
    }

    buildTypes {
        release {
            if (hasKeyStore) {
                signingConfig signingConfigs.release
            } else {
                signingConfig signingConfigs.debug
            }
            
            minifyEnabled true
            shrinkResources true
            
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            
            ndk {
                //noinspection ChromeOsAbiSupport
                abiFilters "x86_64", "armeabi-v7a", "arm64-v8a"
                debugSymbolLevel 'FULL'
            }
            
            crunchPngs true
        }
        
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            minifyEnabled false
            shrinkResources false
            debuggable true
        }
    }

    buildFeatures {
        viewBinding true
        aidl true
        buildConfig false
        renderScript false
        resValues false
        shaders false
    }

    lint {
        checkReleaseBuilds true
        abortOnError true
        warningsAsErrors false
        checkDependencies true
        disable 'MissingTranslation', 'ExtraTranslation'
    }

    packaging {
        resources {
            excludes += [
                '/META-INF/{AL2.0,LGPL2.1}',
                '/META-INF/DEPENDENCIES',
                '/META-INF/LICENSE',
                '/META-INF/LICENSE.txt',
                '/META-INF/license.txt',
                '/META-INF/NOTICE',
                '/META-INF/NOTICE.txt',
                '/META-INF/notice.txt',
                '/META-INF/*.kotlin_module'
            ]
        }
    }
}

flutter {
    source '../..'
}

dependencies {
    // Core libraries from local directory
    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])
    
    // AndroidX and Kotlin dependencies
    implementation libs.gson
    implementation libs.androidx.core.ktx
    implementation libs.androidx.appcompat
    implementation libs.androidx.lifecycle.livedata.ktx
    
    // Desugaring for older Android versions
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.5'
}